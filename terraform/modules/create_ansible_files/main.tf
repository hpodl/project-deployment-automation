locals {
  # Turns a list of ips into a list of host ips and identity files
  hosts_with_keys = [for ip in var.webserver_ips : "${ip} ansible_ssh_private_key_file=${var.private_key_path}"]

  # Turns former into newline separated string of hosts
  newline_separated_hosts = join("\n", local.hosts_with_keys)
}

resource "local_file" "tf_ansible_inventory" {
  filename = "${var.ansible_dir}/inventory"
  content  = <<EOF
# Generated by Terraform
# Ansible inventory containing hosts provided by Terraform
[webservers]
${local.newline_separated_hosts} 
EOF
}

resource "local_file" "tf_ansible_vars" {
  filename = "${var.ansible_dir}/tf_variables.yaml"
  content  = <<EOF
# Generated by terraform
# Variables describing infrastructure provided with Terraform
---
db_user: ${var.db_user}
db_password: ${var.db_passwd}
db_url: ${var.db_url}
EOF
}
